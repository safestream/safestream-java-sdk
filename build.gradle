group 'com.safestream'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'maven-publish'

tasks.withType(JavaCompile) {
    sourceCompatibility = "1.7"
    targetCompatibility = "1.7"
}

repositories {
    mavenCentral()
}

dependencies {
    compile (
            'org.apache.httpcomponents:httpclient:4.5.2',
            'org.apache.commons:commons-io:1.3.2',
            'commons-validator:commons-validator:1.4.0',
            'com.google.code.gson:gson:2.6.1'
    )
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    repositories {
        maven {
            url { "s3://repo.safestream.com/${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases' }" }
            credentials(AwsCredentials) {
                accessKey AWS_ACCESS_KEY
                secretKey AWS_SECRET_KEY
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            groupId 'com.safestream'
            artifactId 'sdk'
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}
